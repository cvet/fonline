// FOS Client Mapper

/*EXPORT*/ class Sprite
{
    Sprite()
    {
        // Empty
    }

    Sprite( string name )
    {
        bool ok = Load( name );
        Assert( ok, name );
    }

    Sprite( uint nameHash )
    {
        bool ok = Load( nameHash );
        Assert( ok, nameHash );
    }

    bool Load( string name )
    {
        Id = ( name.length() > 0 ? LoadSprite( name ) : 0 );
        LoadedNameHash = uint( -1 );
        RefreshData();
        return Id != 0;
    }

    bool Load( uint nameHash )
    {
        if( nameHash == LoadedNameHash )
            return true;

        LoadedNameHash = nameHash;
        Id = LoadSprite( nameHash );
        RefreshData();
        return Id != 0;
    }

    void Unload()
    {
        Id = 0;
        RefreshData();
    }

    // cornerX/Y = -1 left / top, 0 center, 1 right / bottom
    void Draw( int x, int y, int w = 0, int h = 0, int cornerX = -1, int cornerY = -1, int layout = -1, uint color = 0 )
    {
        if( Id == 0 || Hidden )
            return;

        if( w == 0 )
            w = Width;
        if( h == 0 )
            h = Height;

        if( AnimPlay )
        {
            uint ticksPerFrame = Ticks / FrmCount;
            int  framesPassed = ( GetTick() - AnimStartTick ) / ticksPerFrame;
            if( framesPassed > 0 )
            {
                AnimStartTick += framesPassed * ticksPerFrame;
                Frame += ( AnimReverse ? -framesPassed : framesPassed );

                bool endAnim = false;
                if( Frame <= 0 )
                {
                    endAnim = true;
                    AnimStartTick -= -Frame * ticksPerFrame;
                    Frame = 0;
                }
                else if( Frame >= int(FrmCount) - 1 )
                {
                    endAnim = true;
                    AnimStartTick -= ( Frame + 1 - FrmCount ) * ticksPerFrame;
                    Frame = FrmCount - 1;
                }

                if( endAnim )
                {
                    if( AnimLoop )
                        Frame = ( AnimReverse ? int(FrmCount) - 1 : 0 );
                    else
                        AnimPlay = false;
                }
            }
        }

        if( cornerX == 0 )
            x -= w / 2;
        else if( cornerX == 1 )
            x -= w;
        if( cornerY == 0 )
            y -= h / 2;
        else if( cornerY == 1 )
            y -= h;

        switch( layout != -1 ? layout : Layout )
        {
        case IMAGE_LAYOUT_TILE:
            DrawSpritePattern( Id, Frame, x, y, w, h, Width, Height, color );
            break;
        case IMAGE_LAYOUT_CENTER:
            DrawSprite( Id, Frame, x + w / 2 - Width / 2, y + h / 2 - Height / 2, color );
            break;
        case IMAGE_LAYOUT_STRETCH:
            DrawSprite( Id, Frame, x, y, w, h, false, color );
            break;
        case IMAGE_LAYOUT_ZOOM:
            DrawSprite( Id, Frame, x, y, w, h, true, color );
            break;
        default:
            DrawSprite( Id, Frame, x, y, color );
            break;
        }
    }

    void SetFrame( int index )
    {
        Frame = index;
        AnimPlay = false;
    }

    void SetFirstFrame()
    {
        SetFrame( 0 );
    }

    void SetLastFrame()
    {
        SetFrame( int(FrmCount) - 1 );
    }

    void PlayAnimation( bool loop = true, bool reverse = false )
    {
        if( Id == 0 )
            return;
        if( AnimPlay && AnimLoop == loop && AnimReverse == reverse )
            return;

        AnimPlay = true;
        AnimLoop = loop;
        AnimReverse = reverse;
        AnimStartTick = GetTick();
    }

    private void RefreshData()
    {
        if( Id != 0 )
        {
            Width = GetSpriteWidth( Id, 0 );
            Height = GetSpriteHeight( Id, 0 );
            FrmCount = GetSpriteCount( Id );
            Ticks = GetSpriteTicks( Id );
        }
        else
        {
            Width = 0;
            Height = 0;
            FrmCount = 0;
            Ticks = 0;
        }
    }

    uint Id;
    int  Frame = -1;
    int  Layout = IMAGE_LAYOUT_NONE;
    int  Width;
    int  Height;
    uint FrmCount;
    uint Ticks;
    bool AnimPlay;
    bool AnimLoop;
    bool AnimReverse;
    uint AnimStartTick;
    bool Hidden;
    uint LoadedNameHash;
}
