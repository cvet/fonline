// FOS Server Client Sort -5
// Some stuff for work with time.

// GetNearFullSecond
// Result is greather than __FullSecond at least on one.

// For client:
// GetTimeString
// Tags format:
//  %YEAR% - year,
//  %MONTH% - month,
//  %DAY% - day,
//  %DOW% - day of week,
//  %HOUR% - hour,
//  %MINUTE% - minute;
//  %SECOND% - second;
// To show names of values add "-STR" to format tag (%MONTH% -> %MONTH-STR%), supported by month and day of week.
// Example: GetTimeString("%HOUR%:%MINUTE% %DAY% (%DOW-STR%) %MONTH-STR% %YEAR%",fullSecond) -> "14:05 23 (Sunday) November 2299"/"14:05 23 (Воскресенье) Ноябрь 2299".

/*EXPORT*/ uint RealMs( uint value )     { return value * Globals.TimeMultiplier / 1000; }
/*EXPORT*/ uint RealSecond( uint value ) { return value * Globals.TimeMultiplier; }
/*EXPORT*/ uint RealMinute( uint value ) { return value * Globals.TimeMultiplier * 60; }
/*EXPORT*/ uint RealHour( uint value )   { return value * Globals.TimeMultiplier * 3600; }
/*EXPORT*/ uint RealDay( uint value )    { return value * Globals.TimeMultiplier * 86400; }
/*EXPORT*/ uint RealMonth( uint value )  { return value * Globals.TimeMultiplier * 2592000; }
/*EXPORT*/ uint RealYear( uint value )   { return value * Globals.TimeMultiplier * 31536000; }

#define IS_MORNING             # (hour) ( ( hour ) >= 7 && ( hour ) <= 11 )              // 5 hours
#define IS_AFTERNOON           # (hour) ( ( hour ) >= 12 && ( hour ) <= 21 )             // 10 hours
#define IS_NIGHT               # (hour) ( ( hour ) >= 22 || ( hour ) <= 6 )              // 9 hours

#define SECOND_TICKS           # (x) ( ( x ) * 1000 )
#define MINUTE_TICKS           # (x) ( ( x ) * 60000 )
#define HOUR_TICKS             # (x) ( ( x ) * 3600000 )
#define DAY_TICKS              # (x) ( ( x ) * 86400000 )

#define GAME_SECOND_TO_TICK    # (x) ( uint( x ) * 1000 / Globals.TimeMultiplier )
#define GAME_MINUTE_TO_TICK    # (x) ( uint( x ) * 60 * 1000 / Globals.TimeMultiplier )
#define GAME_HOUR_TO_TICK      # (x) ( uint( x ) * 3600 / Globals.TimeMultiplier * 1000 )
#define GAME_DAY_TO_TICK       # (x) ( uint( x ) * 86400 / Globals.TimeMultiplier * 1000 )
#define GAME_MONTH_TO_TICK     # (x) ( uint( x ) * 2592000 / Globals.TimeMultiplier * 1000 )
#define GAME_YEAR_TO_TICK      # (x) ( uint( x ) * 31536000 / Globals.TimeMultiplier * 1000 )

#define STR_MONTH              # (m) ( 20 + ( m ) )
#define STR_DAY_OF_WEEK        # (dow) ( 33 + ( dow ) )

/*EXPORT*/ uint GetNearFullSecond( uint16 year, uint16 month, uint16 day, uint16 hour, uint16 minute, uint16 second )
{
    int jump = 0; // 1 - years, 2 - months, 3 - days
    if( year == 0 )
    {
        year = Globals.Year;
        jump = 1;
    }
    if( month == 0 )
    {
        month = Globals.Month;
        jump = 2;
    }
    if( day == 0 )
    {
        day = Globals.Day;
        jump = 3;
    }

    uint fullSecond = GetFullSecond( year, month, day, hour, minute, second );
    while( fullSecond <= __FullSecond )
    {
        switch( jump )
        {
        case 0:
            return __FullSecond + 1; // Time expired, call faster
        case 1:
            year++;
            break;
        case 2:
            month++;
            if( month > 12 )
            {
                month = 1;
                year++;
            }
            break;
        case 3:
            day++;
            if( day > 28 && day > GetDaysInMonth( year, month ) )
            {
                day = 1;
                month++;
                if( month > 12 )
                {
                    month = 1;
                    year++;
                }
            }
            break;
        default:
            break;
        }

        fullSecond = GetFullSecond( year, month, day, hour, minute, second );
    }
    return fullSecond;
}

/*EXPORT*/ uint GetDaysInMonth( uint16 year, uint16 month )
{
    switch( month )
    {
    case 1:
    case 3:
    case 5:
    case 7:
    case 8:
    case 10:
    case 12: // 31
        return 31;
    case 2:  // 28-29
        if( year % 400 == 0 || ( year % 4 == 0 && year % 100 != 0 ) )
            return 29;
        return 28;
    default: // 30
        return 30;
    }
    return 0;
}

#ifdef __CLIENT
/*EXPORT*/ string GetTimeString( const string format, uint fullSecond ) // Export
{
    uint16 year = 0, month = 0, day = 0, dayOfWeek = 0, hour = 0, minute = 0, second = 0;
    GetGameTime( fullSecond, year, month, day, dayOfWeek, hour, minute, second );
    return GetTimeString( format, year, month, day, dayOfWeek, hour, minute, second );
}

/*EXPORT*/ string GetTimeString( const string format, int year, int month, int day, int dayOfWeek, int hour, int minute, int second ) // Export
{
    string result = format;

    if( year != -1 )
        result = ReplaceText( result, "%YEAR%", year );
    if( month != -1 )
    {
        result = ReplaceText( result, "%MONTH%", month );
        result = ReplaceText( result, "%MONTH-STR%", GetMsgStr( TEXTMSG_GAME, STR_MONTH( month ) ) );
    }
    if( day != -1 )
        result = ReplaceText( result, "%DAY%", day );
    if( dayOfWeek != -1 )
    {
        result = ReplaceText( result, "%DOW%", dayOfWeek );
        result = ReplaceText( result, "%DOW-STR%", GetMsgStr( TEXTMSG_GAME, STR_MONTH( dayOfWeek ) ) );
    }
    if( hour != -1 )
        result = ReplaceText( result, "%HOUR%", hour );
    if( minute != -1 )
        result = ReplaceText( result, "%MINUTE%", minute );
    if( second != -1 )
        result = ReplaceText( result, "%SECOND%", second );

    return result;
}
#endif

// Возвращает представление времени в виде строки в формате dd.mm.yy hh:mm. rifleman17
/*EXPORT*/ string DateTimeToString( uint gameTime )
{
    uint16 year = 0;
    uint16 month = 0;
    uint16 dayOfWeek = 0;
    uint16 day = 0;
    uint16 hour = 0;
    uint16 minute = 0;
    uint16 second = 0;
    GetGameTime( gameTime, year, month, day, dayOfWeek, hour, minute, second );
    return "" + ( day > 9 ? ( "" + day ) : ( "0" + day ) ) + "." + ( month > 9 ? ( "" + month ) : ( "0" + month ) ) + "." + year + " " + ( hour > 9 ? ( "" + hour ) : ( "0" + hour ) ) + ":" + ( minute > 9 ? ( "" + minute ) : ( "0" + minute ) );
}
