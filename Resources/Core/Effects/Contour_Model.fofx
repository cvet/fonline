[Effect]
Passes = 2

[VertexShader]
layout(binding = 0, std140) uniform ProjBuf { mat4 ProjectionMatrix; };

layout(location = 0) in vec2 InPosition;
layout(location = 1) in vec4 InColor;
layout(location = 2) in vec2 InTexCoord;
layout(location = 3) in vec2 InTexEggCoord;

layout(location = 0) out vec4 Color;
layout(location = 1) out vec2 TexCoord;

void main(void)
{
    gl_Position = ProjectionMatrix * vec4(InPosition, 0.0, 1.0);
    Color = InColor;
    TexCoord = InTexCoord;
}

[FragmentShader Pass1]
layout(binding = 0) uniform sampler2D MainTex;
layout(binding = 1, std140) uniform MainTexBuf { vec4 MainTexSize; };
layout(binding = 3, std140) uniform TimeBuf { vec4 FrameTime; vec4 GameTime; };
layout(binding = 2, std140) uniform ContourBuf { vec4 SpriteBorder; };

layout(location = 0) in vec4 Color;
layout(location = 1) in vec2 TexCoord;

layout(location = 0) out vec4 FragColor;

bool CheckPixel(float x, float y)
{
    return texture(MainTex, vec2(x, y)).a == 0.0;
}

void main(void)
{
    float l = TexCoord.x - MainTexSize.z;
    float t = TexCoord.y - MainTexSize.w;
    float r = TexCoord.x + MainTexSize.z;
    float b = TexCoord.y + MainTexSize.w;
    
    vec4 border = SpriteBorder;
    if (border.w < border.y) {
        border.yw = border.wy;
    }
    
    bool checkPixelResult = false;
    if (l < border.x) {
        checkPixelResult = !CheckPixel(r, TexCoord.y);
    }
    else if (r > border.z) {
        checkPixelResult = !CheckPixel(l, TexCoord.y);
    }
    else if (t < border.y) {
        checkPixelResult = !CheckPixel(TexCoord.x, b);
    }
    else if (b > border.w) {
        checkPixelResult = !CheckPixel(TexCoord.x, t);
    }
    else if (texture(MainTex, TexCoord).a > 0.1) {
        checkPixelResult = CheckPixel(l, TexCoord.y) || CheckPixel(r, TexCoord.y) ||
            CheckPixel(TexCoord.x, t) || CheckPixel(TexCoord.x, b);
    }
    
    if (checkPixelResult) {
        FragColor = Color;
        
        if (FragColor.a > 0.1) {
            float v = (TexCoord.y - border.y) / (border.w - border.y);
            v += 1.0 - mod(GameTime.x, 2.0);
            if (v > 1.0) {
                v = 2.0 - v;
            }
            else if (v < 0.0) {
                v = -v;
            }
            FragColor.rgb += v * 0.60 - 0.30;
            FragColor.rgb = clamp(FragColor.rgb, 0.0, 1.0);
        } else {
            FragColor.a = 1.0;
        }
    }
    else {
        discard;
    }
}

[FragmentShader Pass2]
layout(binding = 0) uniform sampler2D MainTex;
layout(binding = 1, std140) uniform MainTexBuf { vec4 MainTexSize; };
layout(binding = 3, std140) uniform TimeBuf { vec4 FrameTime; vec4 GameTime; };
layout(binding = 2, std140) uniform ContourBuf { vec4 SpriteBorder; };

layout(location = 0) in vec4 Color;
layout(location = 1) in vec2 TexCoord;

layout(location = 0) out vec4 FragColor;

bool CheckPixel(float x, float y)
{
    return texture(MainTex, vec2(x, y)).a > 0.0;
}

void main(void)
{
    float l = TexCoord.x - MainTexSize.z;
    float t = TexCoord.y - MainTexSize.w;
    float r = TexCoord.x + MainTexSize.z;
    float b = TexCoord.y + MainTexSize.w;
    
    vec4 border = SpriteBorder;
    if (border.w < border.y) {
        border.yw = border.wy;
    }
    
    bool checkPixelResult = false;
	if(!(l < border.x || r > border.z || t < border.y || b > border.w) && texture(MainTex, TexCoord).a == 0.0) {
		checkPixelResult = CheckPixel(l, TexCoord.y) || CheckPixel(r, TexCoord.y) ||
			CheckPixel(TexCoord.x, t) || CheckPixel(TexCoord.x, b);
    }
    
    if (checkPixelResult) {
        FragColor = Color;
        FragColor.a = 0.5;
        
        if (FragColor.a > 0.1) {
            float v = (TexCoord.y - border.y) / (border.w - border.y);
            v += 1.0 - mod(GameTime.x, 2.0);
            if (v > 1.0) {
                v = 2.0 - v;
            }
            else if (v < 0.0) {
                v = -v;
            }
            FragColor.rgb += v * 0.60 - 0.30;
            FragColor.rgb = clamp(FragColor.rgb, 0.0, 1.0);
        } else {
            FragColor.a = 1.0;
        }
    }
    else {
        discard;
    }
}
