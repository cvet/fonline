// FOS Client Mapper

class Sprite
{
    Sprite()
    {
        // Empty
    }

    Sprite(string name)
    {
        bool ok = Load(name);
        Assert(ok, name);
    }

    Sprite(hstring name)
    {
        bool ok = Load(name);
        Assert(ok, name);
    }

    ~Sprite()
    {
        Unload();
    }

    bool Load(string name)
    {
        return Load(hstring(name));
    }

    bool Load(hstring name)
    {
        if (name == LoadedName) {
            return true;
        }

        Unload();
        LoadedName = name;
        Id = LoadedName != hstring() ? Game.LoadSprite(LoadedName) : 0;
        RefreshData();
        return Id != 0;
    }

    void Unload()
    {
        if (Id != 0) {
            Game.FreeSprite(Id);
        }
        Id = 0;
        RefreshData();
    }

    // cornerX/Y = -1 left / top, 0 center, 1 right / bottom
    void Draw(int x, int y, int w = 0, int h = 0, int cornerX = -1, int cornerY = -1, int layout = -1, uint color = 0)
    {
        if (Id == 0 || Hidden) {
            return;
        }

        if (w == 0) {
            w = Width;
        }
        if (h == 0) {
            h = Height;
        }

        if (cornerX == 0) {
            x -= w / 2;
        }
        else if (cornerX == 1) {
            x -= w;
        }
        if (cornerY == 0) {
            y -= h / 2;
        }
        else if (cornerY == 1) {
            y -= h;
        }

        switch (layout != -1 ? layout : Layout) {
        case IMAGE_LAYOUT_TILE:
            Game.DrawSpritePattern(Id, x, y, w, h, Width, Height, color);
            break;
        case IMAGE_LAYOUT_CENTER:
            Game.DrawSprite(Id, x + w / 2 - Width / 2, y + h / 2 - Height / 2, color, false);
            break;
        case IMAGE_LAYOUT_STRETCH:
            Game.DrawSprite(Id, x, y, w, h, color, false, false);
            break;
        case IMAGE_LAYOUT_ZOOM:
            Game.DrawSprite(Id, x, y, w, h, color, true, false);
            break;
        default:
            Game.DrawSprite(Id, x, y, color, false);
            break;
        }
    }

    void SetFirstFrame()
    {
        Game.StopSprite(Id);
        Game.SetSpriteTime(Id, 0.0);
    }

    void SetLastFrame()
    {
        Game.StopSprite(Id);
        Game.SetSpriteTime(Id, 1.0);
    }

    void PlayAnimation(bool loop = true, bool reverse = false)
    {
        if (Id == 0) {
            return;
        }

        Game.PlaySprite(Id, hstring(), loop, reverse);
    }

    private
    void RefreshData()
    {
        if (Id != 0) {
            Width = Game.GetSpriteWidth(Id);
            Height = Game.GetSpriteHeight(Id);
        }
        else {
            Width = 0;
            Height = 0;
        }
    }

    uint Id;
    int Layout = IMAGE_LAYOUT_NONE;
    int Width;
    int Height;
    uint Ticks;
    bool Hidden;
    hstring LoadedName;
};
